from snakemake.utils import min_version

##### set minimum snakemake version #####
min_version("7.31.1")

# define variables
transcript_gtf = config["transcripts"]
samples = config["samples"]
ref_genome = config["ref_genome"]
out_dir = config["out_dir"]

rule all:
    input:
        expand(os.path.join(out_dir, "proteome", "{sample}", "proteins.fa"), sample=samples)

# extract cDNA from transcripts
rule extract_cDNA:
    input:
        gtf = transcript_gtf
    output:
        transcripts = os.path.join(out_dir, "gffread", "{sample}", "transcripts.fa")
    params:
        guide_fa = ref_genome
    container:
        "docker://quay.io/biocontainers/gffread:0.9.8--0",
    shell:
        """
        gffread -w {output.transcripts} -g {params.guide_fa} {input.gtf}
        """

# extract longest ORFs with transdecoder
rule extract_orfs:
    input:
        fasta = os.path.join(out_dir, "gffread", "{sample}", "transcripts.fa"),
    params:
        out_dir = os.path.join(out_dir, "transdecoder", "{sample}"),
    output:
        dat = os.path.join(out_dir, "transdecoder", "{sample}","transcripts.fa.transdecoder_dir", "base_freqs.dat"),
        cds = os.path.join(out_dir, "transdecoder", "{sample}", "transcripts.fa.transdecoder_dir", "longest_orfs.cds"),
        gff3 = os.path.join(out_dir, "transdecoder", "{sample}", "transcripts.fa.transdecoder_dir", "longest_orfs.gff3"),
        peps = os.path.join(out_dir, "transdecoder", "{sample}", "transcripts.fa.transdecoder_dir", "longest_orfs.pep")

    container:
        "docker://quay.io/biocontainers/transdecoder:5.7.1--pl5321hdfd78af_0"
    shell:
        "TransDecoder.LongOrfs -t {input.fasta} -O {params.out_dir}"

# predict CDS regions with trandecoder
rule predict_cds:
    input:
        peps = os.path.join(out_dir, "transdecoder", "{sample}", "transcripts.fa.transdecoder_dir", "longest_orfs.pep"),
        fasta = os.path.join(out_dir, "gffread", "{sample}", "transcripts.fa")
    output:
        bed = os.path.join(out_dir, "transdecoder", "{sample}","transcripts.fa.transdecoder.bed"),
        cds = os.path.join(out_dir,"transdecoder", "{sample}","transcripts.fa.transdecoder.cds"),
        gff = os.path.join(out_dir,"transdecoder", "{sample}","transcripts.fa.transdecoder.gff3"),
        pep = os.path.join(out_dir,"transdecoder", "{sample}","transcripts.fa.transdecoder.pep"),
    params:
        out_dir = os.path.join(out_dir, "transdecoder", "{sample}"),
    container:
        "docker://quay.io/biocontainers/transdecoder:5.7.1--pl5321hdfd78af_0"
    shell:
        "TransDecoder.Predict -t {input.fasta} -O {params.out_dir} --single_best_only"

# write protein fasta
rule write_protein_fasta:
    input:
        pep = os.path.join(out_dir,"transdecoder", "{sample}","transcripts.fa.transdecoder.pep"),
    output:
        protein_fasta=os.path.join(out_dir, "proteome", "{sample}", "proteins.fa")
    container:
        "docker://quay.io/preskaa/biopython:v240911"
    script:
        "./scripts/write_fasta.py"
